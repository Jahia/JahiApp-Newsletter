###############################################################################
package org.jahia.modules.newsletter.rules

#list any import classes here.
import org.jahia.services.content.rules.*
import org.jahia.services.content.*
import javax.jcr.observation.Event
import org.slf4j.Logger
import java.util.Date
import org.jahia.services.content.*

expander rules.dsl

#declare any global variables here
global User user
global Service service
global Logger logger
###############################################################################

rule "Auto-split subscriptions"
	salience 101
	when
		A new node "j:subscriptions" is created
			- the node has the type jnt:subscriptions
   then
   		Log "Enabling auto-splitting on node " + node.getPath()
		Enable auto-splitting for subnodes of the node into folders of type jnt:subscriptionFolder using configuration "firstChars,j:subscriber,1"
end

rule "Schedule as a newsletter"
    when
		A property j:scheduled has been set on a node
			- the node has the type jnt:newsletterIssue
    then
        Log "Scheduling newsletter " + node.getPath() + " at " + propertyValueAsString
        Execute the action "sendAsNewsletter" at j:scheduled on the node 
end

rule "Cancel newsletter scheduling"
    when
		A property j:scheduled has been removed from a node
			- the node has the type jnt:newsletterIssue
    then
        Log "Canceling newsletter " + node.getPath()
        Cancel execution of action "sendAsNewsletter" on the node 
end

rule "Cancel newsletter scheduling (on delete)"
    when
        A node is deleted
        The node has a parent
            - the parent has the type jnt:newsletter
    then
        Log "Canceling newsletter " + node.getPath()
        Cancel execution of action "sendAsNewsletter" on the node 
end